buildscript {
    repositories {
        mavenCentral()
        jcenter()
        
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.6.RELEASE")
        classpath 'com.bmuschko:gradle-cargo-plugin:2.2.1'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'sonar-runner'
apply plugin: 'com.bmuschko.cargo'

war {
    baseName = 'SpringRest'
    version = '0.0.1-SNAPSHOT'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    // Lombok
	compile("org.projectlombok:lombok:1.16.6")
	// Spring
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework:spring-test:4.2.1.RELEASE")
    compile("org.springframework:spring-context:4.2.1.RELEASE")
    // RestAssured
    compile ('com.jayway.restassured:spring-mock-mvc:2.5.0')
    // Junit
    testCompile("junit:junit")
    // Mockito
    testCompile('org.mockito:mockito-all:1.9.5')
    // Cucumber
    testCompile("info.cukes:cucumber-junit:1.1.8")
    testCompile("info.cukes:cucumber-java:1.1.8")
    testCompile("info.cukes:cucumber-java8:1.2.3");
    // JSON
    testCompile ('com.jayway.jsonpath:json-path:0.8.1')
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'   
    // Log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
    // Cargo
    def cargoVersion = '1.4.5'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
          "org.codehaus.cargo:cargo-ant:$cargoVersion"
}

defaultTasks 'cucumber','test', 'sonarRunner' 

cargo {
    containerId = 'tomcat7x'
    port = 9090
    local {
        installer {
            installUrl = 'http://www.eu.apache.org/dist/tomcat/tomcat-7/v7.0.65/bin/apache-tomcat-7.0.65.zip'
            downloadDir = file("$buildDir/download")
            extractDir = file("$buildDir/extract")
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.projectKey", "SpringRestProject"
        property "sonar.projectName" , "SpringRestProject"
        property "sonar.projectVersion", "1.0"
        property "sonar.host.url", "http://sonarchile.nisum.com"
        property "sonar.jdbc.url", "jdbc:mysql://sonarchile.nisum.com:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonarqubeusr"
        property "sonar.jdbc.password", "s0n4rus3r"
    }
}

test{
    exclude 'com/demo/cucumber/**'
}

cargoStartLocal.dependsOn war

task cucumber(type: Test, dependsOn: [testClasses, cargoStartLocal]) {
    exclude 'com/demo/junit/**'
    
    include 'com/demo/cucumber/**'
}

tasks.withType(Test) {
    testLogging {
        events 'started', 'passed' 
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
